plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}
subprojects {
    group = 'com.server'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'com.google.guava:guava:31.1-jre'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
        compileOnly 'org.projectlombok:lombok'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        testCompileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testAnnotationProcessor 'org.projectlombok:lombok'
    }
}

project(':core') {
    dependencies {
        compileOnly('org.springframework.boot:spring-boot-starter-test')
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(":common") {
    dependencies {
        implementation(project(':core'))
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':customer') {
    dependencies {
        implementation(project(':core'))
        implementation(project(':common'))
        implementation('org.flywaydb:flyway-core')
        implementation group: 'commons-codec', name: 'commons-codec', version: '1.9'
        implementation 'commons-beanutils:commons-beanutils:1.9.4'
        implementation 'org.apache.commons:commons-collections4:4.4'
        implementation 'commons-lang:commons-lang:2.6'
        implementation 'com.ardikars.common:common-logging:1.3.3.RELEASE'
        implementation 'net.sf.ezmorph:ezmorph:1.0.6'

        compileOnly('net.sf.json-lib:json-lib:2.4:jdk15@jar')
    }

    bootJar {
        enabled = true
    }

    jar {
        enabled = true
    }

    tasks.named("bootJar") {
        archiveFileName.set("server-customer.jar")
    }

    tasks.register("zip", Zip.class) {
        dependsOn("bootJar")
        archiveFileName.set("server-customer.zip")

        from("build/libs/server-customer.jar") { into("") }
        from("../.platform") { into(".platform") }
        from("../procfiles/customer/Procfile") { into("") }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':shop') {
    dependencies {
        implementation(project(':core'))
        implementation(project(':common'))
        implementation 'net.nurigo:sdk:4.2.4'
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
